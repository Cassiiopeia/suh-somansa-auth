plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.4'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'maven-publish'
}

group = 'me.suhsaechan'
version = '1.0.7'
//version = project.hasProperty('version') ? project.property('version') : '1.0.0'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
    withSourcesJar()
    withJavadocJar()
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    implementation 'com.squareup.okhttp3:okhttp-urlconnection:4.12.0'
    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation 'com.fasterxml.jackson.core:jackson-databind'
    testImplementation 'com.fasterxml.jackson.core:jackson-core'
    testImplementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
}

tasks.named('test') {
    useJUnitPlatform()
}

// Nexus 저장소에 배포 설정
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = 'suh-somansa-auth'
                description = '소만사 인증 확인 라이브러리'
                url = 'https://github.com/suhsaechan/suh-somansa-auth'
            }
        }
    }
    repositories {
        mavenLocal()
        maven {
            name = 'nexus'
            def nexusUrl = project.findProperty('nexusUrl') ?: 'suh-project.synology.me'
            def nexusPort = project.findProperty('nexusPort') ?: '9999'
            def releasesRepoUrl = "http://${nexusUrl}:${nexusPort}/repository/maven-releases/"
            def snapshotsRepoUrl = "http://${nexusUrl}:${nexusPort}/repository/maven-snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            allowInsecureProtocol = true

            if (project.hasProperty('nexusUsername') && project.hasProperty('nexusPassword')) {
                credentials {
                    username = project.property('nexusUsername')
                    password = project.property('nexusPassword')
                }
            }
        }
    }
}

// JAR 파일만 생성하는 태스크 (bootJar 비활성화)
bootJar {
    enabled = false
}

jar {
    enabled = true
}
